import org.gradle.api.JavaVersion

plugins {
    id 'java-library'
    id 'org.jsonschema2pojo' version '1.2.1'
    id 'maven-publish'
}

group = 'de.ukw.ccc'
version = '0.2.0'

repositories {
    mavenCentral()
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

dependencies {
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.12.7.1'
    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.9.3'
    testImplementation 'org.assertj:assertj-core:3.24.2'
}

test {
    useJUnitPlatform()
}

jsonSchema2Pojo {
    annotationStyle = 'jackson'
    sourceType = 'jsonschema'
    includeAdditionalProperties = false
    includeConstructors = true
    generateBuilders = true
    useInnerClassBuilders = true
    constructorsRequiredPropertiesOnly = true
    includeSetters = true
    includeJsonTypeInfoAnnotation = false
    refFragmentPathDelimiters = "#/."
    includeToString = false

    targetPackage = 'de.ukw.ccc.bwhc.dto'
    targetVersion = "11"
}

jar.dependsOn(generateJsonSchema2Pojo)

publishing {
    repositories {
        maven {
            name = "GitDnpmDev"

            def releasesRepoUrl = uri('https://git.dnpm.dev/api/packages/public/maven')
            def snapshotsRepoUrl = uri('https://git.dnpm.dev/api/packages/public-snapshots/maven')
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl

            credentials(HttpHeaderCredentials) {
                name = "Authorization"
                value = "token ${dnpm_dev_token}"
            }

            authentication {
                header(HttpHeaderAuthentication)
            }
        }
    }

    publications {
        maven(MavenPublication) {
            from components.java
        }
    }
}